@create Operations Console Parent
@set Operations Console Parent = NO_COMMAND
@desc Operations Console Parent=A large console containing controls for the Operations on board the vessel.
@set Operations Console Parent/DECRIBE = NO_COMMAND VISUAL
&CMD-BAYSTAT Operations Console Parent=$baystat:th [if(hs_get_attr(me,ispowered),pemit(%#,ansi(hb,.[repeat(-,68)].%r|)[ljust(Bay Status,30)][rjust(hs_get_attr(me,name)%([hs_get_attr(me,ident)]%),38)][ansi(hb,|%r%b>[repeat(-,66)]<%r|)][ljust(ansi(hg,%[)[ansi(hw,#)][ansi(hg,%]%b[ljust(Bay Name,38)]%b[ljust(Bay Doors,12)]%b[ljust(Code,12)])],68)][ansi(hb,|%r)][iter(hs_get_attr(me,landinglocs),ansi(hb,|)[ljust(rjust(#@,2)%b%b[ljust(name(##),38)]%b[ljust(switch(hs_get_attr(##,Active),1,Open,0,Closed),12)]%b[ljust(switch(hs_get_attr(##,CODE),,None,ansi(hr,#$)),12)],68)][ansi(hb,|%r)],,)][ansi(hb,'[repeat(-,68)]')]),pemit(%#,ansi(hg,-) [name(me)] is currently powered down.))]
&CMD-BAYSTAT2 Operations Console Parent=$baystat *:th [if(hs_get_attr(me,ispowered),if(isdbref(setr(0,extract(hs_get_attr(me,landinglocs),%0,1))),pemit(%#,ansi(hb,.[repeat(-,68)].%r|)[ljust(Bay Status,30)][rjust(hs_get_attr(me,name)%([hs_get_attr(me,ident)]%),38)][ansi(hb,|%r%b>[repeat(-,66)]<%r|)][ljust(ansi(hg,ljust(Vessel Name,25)%b[ljust(Class Name,25)]%b[ljust(Active,7)]%b[ljust(Secure,7)]),68)][ansi(hb,|%r)][iter(lcon(%q0),if(hasflag(##,HSPACE_OBJECT),ansi(hb,|)[ljust(ljust(hs_get_attr(##,name),25)%b[ljust(hs_get_attr(##,CLASSNAME),25)]%b[ljust(switch(hs_sys_attr(##/reactor,current output,1),>0,Yes,No),7)]%b[ljust(switch(hs_get_attr(##,BOARDING CODE),,No,Yes),7)],68)][ansi(hb,|%r)]),,)][ansi(hb,'[repeat(-,68)]')]),pemit(%#,ansi(hg,-) Invalid bay.)),pemit(%#,ansi(hg,-) [name(me)] is currently powered down.))]
&CMD-CLOSE_BAY Operations Console Parent=$close bay *:th if(hs_get_attr(me,ispowered),if(isdbref(setr(0,extract(hs_get_attr(me,landinglocs),%0,1))),if(hs_get_attr(%q0,active),hs_set_attr(%q0/active,0)[pemit(%#,ansi(hg,-) Bay doors to bay %0 closed.)][remit(%q0,The bay doors are slowly closed.)],pemit(%#,ansi(hg,-) Bay doors already closed.)),pemit(%#,ansi(hg,-) Invalid bay.)),pemit(%#,ansi(hg,-) [name(me)] is currently powered down.))
&CMD-CTRANSFER Operations Console Parent=$ctransfer * *=*:@swi [u(fn_contact,%2)]=,@pemit %#=[ansi(hg,-)] No such contact on sensors.,{th [switch(dist3d(round(hs_get_attr(me,x),0),round(hs_get_attr(me,y),0),round(hs_get_attr(me,z),0),round(hs_get_attr(setr(7,extract(#$,2,1,:)),x),0),round(hs_get_attr(%q7,y),0),round(hs_get_attr(%q7,z),0)),<3,if(t(setr(3,grab(commodities(),%1*,|))),switch(cargotransfer(v(HSDB_OWNER),%q7,%0,%q3),#-1*,pemit(%#,ansi(hg,-) Invalid amount.),#-2*,pemit(%#,Weird, check with a wizard. Didn't work.),#-3*,pemit(%#,Again, odd stuff is going on.),#-4*,pemit(%#,Welp, I'm not setup right.),#-5*,pemit(%#,ansi(hg,-) We do not have enough %q3 on board.),#-6*,pemit(%#,ansi(hg,-) Target vessel does not have enough space.),pemit(%#,ansi(hg,-) %0 %q3 transferred to the [name(%q7)].)),pemit(%#,ansi(hg,-) Invalid commodity.)),pemit(%#,ansi(hg,-) Contact out of range. %(Must be within 2 HM%)))]}
&CMD-OPEN_BAY Operations Console Parent=$open bay *:th if(hs_get_attr(me,ispowered),if(isdbref(setr(0,extract(hs_get_attr(me,landinglocs),%0,1))),if(hs_get_attr(%q0,active),pemit(%#,ansi(hg,-) Bay doors not closed.),hs_set_attr(%q0/active,1)[pemit(%#,ansi(hg,-) Bay doors to bay %0 opened.)][remit(%q0,The bay doors are slowly opened.)]),pemit(%#,ansi(hg,-) Invalid bay.)),pemit(%#,ansi(hg,-) [name(me)] is currently powered down.))
&CMD-SET_CODE Operations Console Parent=$set code *=*:th if(hs_get_attr(me,ispowered),if(isdbref(setr(0,extract(hs_get_attr(me,landinglocs),%0,1))),switch(%1,,pemit(%#,ansi(hg,-) Bay access code cleared.),pemit(%#,ansi(hg,-) Bay access code set.))[hs_set_attr(%q0/code,%1)],pemit(%#,ansi(hg,-) Invalid bay.)),pemit(%#,ansi(hg,-) [name(me)] is currently powered down.))
&CMD_BLINK Operations Console Parent=$link *=*/*:@swi [u(fn_contact,%0)]={},@pemit %#=[ansi(hg,-)] No such contact on sensors.,{@swi [get([extract([u(fn_contact,%0)],2,1,:)]/deadman)]=YES,@pemit %#=[ansi(hg,-)] That ship is deadmanned and may not be boarded.,@nav/boardlink %0/%1=%2}
&CMD_CPOWER_OFF Operations Console Parent=$cpower off:@console/power 0
&CMD_CPOWER_ON Operations Console Parent=$cpower on:@console/power 1
&CMD_HSTAT Operations Console Parent=$hstat:@nav/hstat
&CMD_ICOM Operations Console Parent=$icom *:th [u(fn_notify_consoles,[v(hsdb_owner)],[ansi(c,\[ICOM - [name(me)]\])] %0)]
&CMD_SREP Operations Console Parent=$srep:&deadman [v(hsdb_owner)];@nav/sensorreport 0
&CMD_UNLINK Operations Console Parent=$unlink *:@nav/boardunlink %0
@FAILURE Operations Console Parent=It's secured to the floor.
@set Operations Console Parent/FAILURE=no_command
&FN_CONTACT Operations Console Parent=[squish([iter([hs_srep(me,0)],[switch([extract(##,1,1,:)],%0,##)],|)])]
&FN_NOTIFY_CONSOLES Operations Console Parent=[pemit([iter(hs_get_attr(%0,consoles)],[switch([loc([lock(##/use)])],[loc(##)],[lock(##/use)])])],%1)]